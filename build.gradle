buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.2.1.RELEASE'
        classpath 'com.andrewkroh.gradle:gradle-protobuf-plugin:0.4.0'
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'spring-boot'
apply plugin: 'idea'
apply plugin: 'protobuf'

sourceCompatibility = 1.7
version = '1.0'

def protoSrc = new File(buildDir, protobuf.outputJava)

sourceSets {
    main {
        java {
            srcDir protoSrc
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-websocket'
    compile 'org.springframework:spring-jms'
    compile 'org.apache.activemq:activemq-broker'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

protobuf {
    src = 'src/main/resources/proto'
    version = '2.5.0'
    compiler = protobufCompiler
}

idea.module {
    excludeDirs -= file(buildDir)
    buildDir.listFiles({ it != protoSrc.parentFile } as FileFilter).each { excludeDirs += it }
    protoSrc.parentFile.listFiles({ it.name != 'java' } as FileFilter).each { excludeDirs += it }
}

wrapper {
  gradleVersion = '2.3'
}
